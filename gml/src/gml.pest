COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/"
           | "//" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " "+ }
tokens = _{ SOI ~ token* ~ EOI }
stmts = _{ SOI ~ stmt* ~ EOI }

// statements
stmt              = _{ if_stmt
                     | repeat_stmt
                     | while_stmt
                     | for_stmt
                     | with_stmt
                     | return_stmt
                     | exit_stmt
                     | block_stmt
                     | var_stmt
                     | assign_stmt
                     | expr_stmt
                     | empty_stmt }
  if_stmt         = { "if" ~ expr ~ nl ~ stmt ~ ("else" ~ nl ~ stmt)? }
  repeat_stmt     = { "repeat" ~ expr ~ nl ~ stmt }
  while_stmt      = { "while" ~ expr ~ nl ~ stmt }
  for_stmt        = { "for" ~ "(" ~ assign_expr ~ ";" ~
                      expr ~ ";" ~ (assign_expr | expr) ~ ")" ~ nl ~ stmt }
  with_stmt       = { "with" ~ expr ~ nl ~ stmt }
  return_stmt     = { "return" ~ expr ~ sep }
  exit_stmt       = { "exit" ~ sep }
  block_stmt      = { "{" ~ stmt* ~ "}" ~ nl }
  var_stmt        = { "var" ~ id ~ sep }
  assign_stmt     = { assign_expr ~ sep }
  expr_stmt       = { expr ~ sep }
  empty_stmt      = { sep }

nl                = _{ NEWLINE* }
sep               = _{ ";"? ~ (NEWLINE+ | (NEWLINE* ~ EOI)) }

assign_expr       = { assign_lhs ~ assign_op ~ expr }
  assign_lhs      = { (var | assign_id) ~ (member | index)* }
    assign_id     = { "(" ~ expr ~ ")" }
  assign_op       = _{ assign | add_assign | sub_assign | mul_assign | div_assign }
    assign        = { "=" }
    add_assign    = { "+=" }
    sub_assign    = { "-=" }
    mul_assign    = { "*=" }
    div_assign    = { "/=" }

// exprs (uses PrattParser)
expr          = { prefix* ~ primary ~ postfix* ~ (infix ~ prefix* ~ primary ~ postfix*)* }
  prefix      = _{ not | pos | neg | bit_not | pre_incr | pre_decr }
    not       = @{ "!" }
    pos       = @{ "+" }
    neg       = @{ "-" }
    bit_not   = @{ "~" }
    pre_incr  = @{ "++" }
    pre_decr  = @{ "--" }
  infix       = _{ and | or | xor
                 | bit_and | bit_or | bit_xor
                 | le | lt | ge | gt | ne | eq
                 | add | sub | mul | div | idiv | imod }
    and       = @{ "&&" | "and" }
    or        = @{ "||" | "or" }
    xor       = @{ "^^" }
    bit_and   = @{ "&" }
    bit_or    = @{ "|" }
    bit_xor   = @{ "^" }
    le        = @{ "<=" }
    lt        = @{ "<" }
    ge        = @{ ">=" }
    gt        = @{ ">" }
    ne        = @{ "!=" }
    eq        = @{ "==" | "=" }
    add       = @{ "+" }
    sub       = @{ "-" }
    mul       = @{ "*" }
    div       = @{ "/" }
    idiv      = @{ "div" }
    imod      = @{ "%" | "mod" }
  postfix     = _{ member | index | post_incr | post_decr }
    member    = { "." ~ id }
    index     = { "[" ~ expr_list ~ "]" }
    post_incr = @{ "++" }
    post_decr = @{ "--" }
primary = _{ "(" ~ expr ~ ")" | call_expr | var | float | int | str }

var = { (global ~ "." )? ~ id }
  global = @{ "global" }

call_expr = { id ~ "(" ~ expr_list? ~ ")" }
expr_list = _{ expr ~ ("," ~ expr)* }


// tokens
token = { keyword | id | float | int | str | op }
keyword = @{ ("if" | "else" | "for" | "repeat" | "with" | "var" | "global"
             | "do" | "until" | "switch" | "break" | "continue" | "exit"
             | "begin" | "end"
             | "try" | "catch" | "finally" | "throw" | "new" | "delete"
             | "and" | "or" | "div" | "mod" | "return")
           ~ !ASCII_ALPHANUMERIC
           }
op = @{ "&&" | "&" | "||" | "|" | "^^" | "^"
      | "<=" | "<<" | "<" | ">=" | ">>" | ">"
      | "++" | "+" | "--" | "-" | "*" | "/"
      | "==" | "=" | "!=" | "!"
      | ";" | "." | ","
      | "(" | ")" | "{" | "}" | "[" | "]" }
id = @{ !keyword ~ ("_" | ASCII_ALPHA) ~ ("_" | ASCII_ALPHANUMERIC)* }
float = @{ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+ }
int = @{ ASCII_DIGIT+ }
str = @{ "\"" ~ (!"\"" ~ ANY | "\\\"")* ~ "\"" }
